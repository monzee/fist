plugins {
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

dependencies {
    testImplementation 'junit:junit:4.12'
}

javadoc {
    title = 'Fist Core'
    options.links 'https://developer.android.com/reference'
    options.encoding 'UTF-8'
}

task jarSources(type: Jar, dependsOn: 'classes', group: 'publishing') {
    classifier 'sources'
    from sourceSets.main.allSource
}

task jarJavadoc(type: Jar, dependsOn: 'javadoc', group: 'publishing') {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories.maven {
        name 'bintray'
        url 'https://api.bintray.com/maven/monzee/jvm/fist-core/'
        credentials {
            username = BINTRAY_USER
            password = BINTRAY_API_KEY
        }
    }

    publications {
        core(MavenPublication) {
            from components.java
            artifact jarSources
            artifact jarJavadoc
            pom {
                name = 'Fist Core'
                description = 'Abstract finite state transducer'
                url = 'https://github.com/monzee/fist'
                developers {
                    developer {
                        id = 'monzee'
                        name = 'Mon Zafra'
                        email = 'mz@codeia.ph'
                        organization = 'Codeia Inc'
                        organizationUrl = 'https://codeia.ph'
                    }
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://mit-license.org/license.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/monzee/fist'
                    connection = 'scm:git:https://github.com/monzee/fist.git'
                    developerConnection = 'scm:git:ssh://git@github.com:monzee/fist.git'
                }
            }
        }
    }
}
