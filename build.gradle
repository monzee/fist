buildscript {
    ext {
        kotlin_version = '1.2.10'
        coroutines_version = '0.20'
    }
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
        maven { url 'https://kotlin.bintray.com/kotlinx' }
     }

     group = 'ph.codeia.fist'
     version = LIBRARY_VERSION
 }

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def node(Closure c) {
    c.resolveStrategy = Closure.DELEGATE_FIRST
    c
}

static def pomMetadata(meta) {
    node {
        ['name', 'description', 'url'].each { it in meta && "$it"(meta[it]) }

        developers {
            developer {
                id 'monzee'
                name 'Mon Zafra'
                email 'mz@codeia.ph'
                organization 'Codeia Inc'
                organizationUrl 'https://codeia.ph'
            }
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://mit-license.org/license.txt'
                distribution 'repo'
            }
        }

        scm {
            url 'https://github.com/monzee/fist'
            connection 'scm:git:https://github.com/monzee/fist.git'
            developerConnection 'scm:git:ssh://git@github.com:monzee/fist.git'
        }
    }
}
