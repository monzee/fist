apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "v$LIBRARY_VERSION"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(':fist-core')
    compileOnly 'androidx.appcompat:appcompat:1.1.0-alpha01'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

publishing {
    repositories.maven {
        name 'bintray'
        url 'https://api.bintray.com/maven/monzee/jvm/fist-android/'
        credentials {
            username = BINTRAY_USER
            password = BINTRAY_API_KEY
        }
    }

    android.libraryVariants.matching { it.buildType.name == 'release' }.all {
        def stem = name.capitalize()
        def hardDeps = variantData.variantDependency.apiElements.allDependencies
        def allDeps = variantData.variantDependency.runtimeElements.allDependencies
        def docs = task(
            "javadoc$stem",
            dependsOn: ':fist-core:javadoc',
            type: Javadoc,
            group: 'documentation'
        ) {
            source = javaCompile.source
            classpath += javaCompile.classpath + files(android.bootClasspath)
            title = 'Fist Android'
            options {
                links '../../../../fist-core/build/docs/javadoc'
                links 'https://developer.android.com/reference'
            }
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }

        publications.create(name, MavenPublication) {
            artifact source: outputs[0].outputFile, builtBy: assemble

            artifact task("jar${stem}Sources", type: Jar, group: 'publishing') {
                classifier 'sources'
                from javaCompile.source
            }

            artifact task("jar${stem}Javadoc", type: Jar, dependsOn: docs, group: 'publishing') {
                classifier 'javadoc'
                from docs.destinationDir
            }

            pom.withXml {
                def contents = asNode().children().last()
                contents + pomMetadata(
                    name: 'Fist Android',
                    description: 'Android implementation of the async abstract machines and samples',
                    url: 'https://github.com/monzee/fist'
                )
                contents + node {
                    dependencies {
                        allDeps.each { dep ->
                            dependency {
                                groupId dep.group
                                artifactId dep.name
                                version dep.version
                                if (!(dep in hardDeps)) {
                                    scope 'provided'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
